plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

android {
    compileSdk 33

    defaultConfig {
        minSdk 19
        targetSdk 31

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        resourcePrefix 'bitmap_monitor_'

        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        prefab true
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation 'com.bytedance.android:shadowhook:1.0.3'
}

if (project.hasProperty("uploadToMavenCentral") && project.property("uploadToMavenCentral")) {
    publishing {
        publications {
            release(MavenPublication) {
                pom {
                    name = 'AndroidBitmapMonitor'
                    description = 'A bitmap memory monitor library for android development.'
                    url = 'https://github.com/shixinzhang/AndroidBitmapMonitor'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'shixinzhang'
                            name = 'shixinzhang'
                            email = 'shixinzhang2016@gmail.com'
                        }
                    }

                    scm {
                        connection = 'https://github.com/shixinzhang/AndroidBitmapMonitor.git'
                        developerConnection = 'https://github.com/shixinzhang/AndroidBitmapMonitor.git'
                        url = 'https://github.com/shixinzhang/AndroidBitmapMonitor'
                    }
                }

                groupId "io.github.shixinzhang"
                artifactId "android-bitmap-monitor"
                version "1.1.0"

                afterEvaluate {
                    from components.release
                }
            }
        }
        repositories {
            maven {
                url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username ossrhUsername // ossrhUsername is your sonatype username
                    password ossrhPassword // ossrhUsername is your sonatype password
                }
            }
        }
    }

    signing {
        sign publishing.publications.release
    }
}